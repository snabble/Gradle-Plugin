plugins {
    id 'kotlin'
    id 'maven-publish'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.21.0'
}

group 'io.snabble'
version '1.0.0'

pluginBundle {
    website = 'https://docs.snabble.io/docs/android/'
    vcsUrl = 'https://github.com/snabble/Gradle-Plugin.git'
    tags = ['snabble', 'self-scanning']
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'io.snabble'
            artifactId 'setup'
            version version

            from components.java
        }
    }
}

dependencies {
    implementation(gradleApi())
    implementation(localGroovy())
    api "org.jetbrains.kotlin:kotlin-stdlib"
    api "com.squareup.okhttp3:okhttp:4.10.0"
    compileOnly 'com.android.tools.build:gradle:7.1.0'
}

gradlePlugin {
    plugins {
        setup {
            id = 'io.snabble.setup'
            displayName = 'Snabble Setup Helper'
            description = 'Plugin for simlified configutation of the Snabble Android SDK and metadata downloader'
            implementationClass = 'io.snabble.setup.SnabblePlugin'
        }
    }
}

repositories {
    mavenCentral()
    mavenLocal()
    google()
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin/'
}

task generateResources {
    ext {
        propFile = file("$buildDir/generated/snabble.properties")
    }
    outputs.file propFile
    doLast {
        mkdir propFile.parentFile
        propFile.text = "version=$project.version"
    }
}

processResources {
    from files(generateResources)
}